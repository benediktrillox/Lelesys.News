# News list
prototype(Lelesys.News:List) {
	# default templatePath is below which is set automatically by Neos.
	# to change template override templatePath
	# templatePath = 'resource://Lelesys.News/Private/Templates/NodeTypes/List.html'

	# newsFolder is read from current node's newsFolder property if exists
	# otherwise the closest Folder node is selected
	@context.newsFolderNode = ${q(node).property('newsFolder') != null ? q(node).property('newsFolder') : q(node).closest('[instanceof Lelesys.News:Folder]').get(0)}

	# configuration merged from Settings and from the node properties.
	# this way configuration can be changed globally for all sites in Settings.yaml OR
	# change configuration in your Site package's TypoScript by overriding the values OR
	# if you set a new value for the configuration in the node property then it is only available
	# for particular view. in this case if you do any change in Settings or TypoScript level
	# then it will still take the configuration value from the node properties.
	@context.configuration = ${Configuration.setting('Lelesys.News.view.list')}
	@context.configuration.@process.mergeConfiguration = ${NewsConfiguration.mergeWithNodeProperties(value, node)}
	configuration = ${configuration}

	# TODO - remove the condition if Content
	newsCollection = ${q(node).is('[instanceof Neos.Neos:Content]') ? q(newsFolderNode).children('[instanceof Lelesys.News:News]').get() : newsFolderNode.children('[instanceof Lelesys.News:News]').get()}

	# Filter and sorting
	@context.filterByCategories = ${q(node).is('[instanceof Lelesys.News:List]') ? q(node).property_array('filterByCategories') != null ? Array.join(q(node).property_array('filterByCategories')) : '' : ''}
	@context.filterByTag = ${request.arguments.tag}
	@context.filterByYear = ${request.arguments.year}
	@context.filterByMonth = ${request.arguments.month}

	# 1. filter the news collection by one or many categories if applicable
	# this internally uses the extended filter() operation
	newsCollection.@process.filterByCategories = ${filterByCategories != null && filterByCategories != '' ? q(value).filter('[categories *= \'' + filterByCategories + '\']').get() : value}

	# 2. filter the news collection by given year if applicable
	newsCollection.@process.filterByYear = ${filterByYear != null && filterByMonth == null ? q(value).filter('[dateTime = \'' + filterByYear + '\']', 'Y').get() : value}

	# 3. filter the news collection by given year and month if applicable
	newsCollection.@process.filterByYearAndMonth = ${filterByYear != null && filterByMonth != null ? q(value).filter('[dateTime = \'' + filterByYear + '/' + filterByMonth + '\']', 'Y/m').get() : value}

	# 4. filter the news collection by a tag if applicable
	@context.configuration = ${this.configuration}

	newsCollection = Lelesys.News:NewsCollector
	newsCollection.@if.newsFolderFound = ${newsFolderNode != null}

	# this renders a single item inside the list
	newsItem = Lelesys.News:News
	newsItem {
		templatePath = 'resource://Lelesys.News/Private/Templates/TypoScriptObjects/ListItem.html'
	}

	# uncached the list view
	@cache {
		mode = 'uncached'
		context {
			1 = 'node'
			2 = 'documentNode'
		}
	}
}
